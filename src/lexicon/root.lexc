!!! Root of Japanese Lexicon - root.lexc !!!

Multichar_Symbols +Anim -Anim +Nom +Gen +Dat +Acc +Pres +Past +Dobj +DobjNom -Mod +Pass +Neg +Polite

LEXICON Root
Name ;
Noun ;
Verb ;

LEXICON Noun
book:本         Ninf-A;
dog:犬          Ninf+A;
ramen:ラーメン  Ninf-A;
apple:りんご    Ninf-A;
English:英語    Ninf-A;
Japanese:日本語 Ninf-A;

LEXICON Name
Mary:メアリー  NameInfFem;
John:ジョン    NameInfMasc;
Ando:安藤      NameInfMasc;
Michiko:みちこ NameInfFem;

LEXICON NameInfFem
0            NameInf;
_chan:ちゃん Ninf+A;

LEXICON NameInfMasc
0         NameInf;
_kun:くん Ninf+A;

LEXICON NameInf
0            Ninf+A;
_san:さん    Ninf+A;
_sensei:先生 Ninf+A;

LEXICON Ninf+A
+Anim:0 Ninf;

LEXICON Ninf-A
-Anim:0 Ninf;

LEXICON Ninf
+Nom:が NP;
+Gen:の NP;
+Dat:に NP;
+Acc:を NP;

LEXICON NP
+NP:0 #;

LEXICON Verb
\x.Exists(x)-Dobj-Anim:あ           v5r_base;
\x.Exists(x)-Dobj+Anim:い           v1_base;
\x\y.Eats(y,x)+Dobj:食べ            v1_base;
\x\y.Understands(y,x)+DobjNom:わか  v5r_base;
\x\y.Speaks(y,x)+Dobj:話            v5s_base;

LEXICON v1_base
-Vmod:0    v1_mod;
+Pass:られ v1_mod;

LEXICON v1_mod
0  v1_plain;
0  v_masu_stem;

LEXICON v1_plain
+Pres:る V;
+Past:た V;

LEXICON v5r_base
-Vmod:0    v5r_mod;
+Pass:られ v1_mod;

LEXICON v5r_mod
0    v5r_plain;
0:り v_masu_stem;

LEXICON v5r_plain
+Pres:る   V;
+Past:った V;

LEXICON v_masu_stem
+Polite+Pres:ます V;
+Polite+Past:ました V;
+Polite+Neg+Pres:ません V;
+Polite+Neg+Past:ませんでした V;

LEXICON v5s_base
-Vmod:0    v5s_mod;
+Pass:され v1_mod;

LEXICON v5s_mod
0    v5s_plain;
0:し v_masu_stem;

LEXICON v5s_plain
+Pres:す   V;
+Past:した V;

LEXICON V
+V:0 #;

